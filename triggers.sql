--Trigger per aggiornare il timestamp del completamento pagamento
CREATE OR REPLACE TRIGGER TRG_PAGAMENTI_COMPLETATO
    AFTER UPDATE OF STATO_PAGAMENTO ON PAGAMENTI
    FOR EACH ROW
BEGIN
    IF :OLD.STATO_PAGAMENTO != 'COMPLETATO' AND :NEW.STATO_PAGAMENTO = 'COMPLETATO' THEN	
        UPDATE PRENOTAZIONI 
        SET DATA_TRANSAZIONE_COMPLETATA = CURRENT_TIMESTAMP,
            STATO_PRENOTAZIONE = 'CONFERMATA'
        WHERE ID_PRENOTAZIONE = :NEW.PRENOTAZIONI_ID_PRENOTAZIONE;
    END IF;
END;



--TRIGGER PER INIZIALIZZARE I POSTI DISPONIBILI (in base al totale del veicolo) PER OGNI CORSA
create or replace TRIGGER TRG_CORSE_POSTI_INIT
    BEFORE INSERT ON CORSE
    FOR EACH ROW
DECLARE
    posti_totali_init NUMBER(3);
    posti_disabili_init NUMBER(2);
BEGIN
    SELECT POSTI_TOTALI, POSTI_DISABILI 
    INTO posti_totali_init, posti_disabili_init
    FROM VEICOLI 
    WHERE ID_VEICOLO = :NEW.VEICOLI_ID_VEICOLO;

    IF :NEW.POSTI_DISPONIBILI IS NULL THEN
        :NEW.POSTI_DISPONIBILI := posti_totali_init;
    END IF;

    IF :NEW.POSTI_DISABILI_DISPONIBILI IS NULL THEN
        :NEW.POSTI_DISABILI_DISPONIBILI := posti_disabili_init;
    END IF;
END;


-- Trigger per aggiornare i posti disponibili dopo prenotazione
CREATE OR REPLACE TRIGGER TRG_PRENOTAZIONI_POSTI
    AFTER INSERT ON PRENOTAZIONI
    FOR EACH ROW
BEGIN
    UPDATE CORSE 
    SET POSTI_DISPONIBILI = POSTI_DISPONIBILI - :NEW.NUMERO_BIGLIETTI,
        POSTI_DISABILI_DISPONIBILI = POSTI_DISABILI_DISPONIBILI - :NEW.NUMERO_BIGLIETTI_DISABILI
    WHERE ID_CORSA = :NEW.CORSE_ID_CORSA;
END;

-- Trigger per aggiornare i posti disponibili dopo cancellazione prenotazione
CREATE OR REPLACE TRIGGER TRG_PRENOTAZIONI_CANCELLAZIONE
    AFTER DELETE ON PRENOTAZIONI
    FOR EACH ROW
BEGIN
    UPDATE CORSE 
    SET POSTI_DISPONIBILI = POSTI_DISPONIBILI + :OLD.NUMERO_BIGLIETTI,
        POSTI_DISABILI_DISPONIBILI = POSTI_DISABILI_DISPONIBILI + :OLD.NUMERO_BIGLIETTI_DISABILI
    WHERE ID_CORSA = :OLD.CORSE_ID_CORSA;
END;



--TRIGGER CALCOLO DURATA VIAGGIO
CREATE OR REPLACE TRIGGER TRG_CORSE_CALCOLA_DURATA 
BEFORE INSERT OR UPDATE OF ORARIO_PARTENZA, ORARIO_ARRIVO ON CORSE
FOR EACH ROW
BEGIN
    IF :NEW.ORARIO_PARTENZA IS NOT NULL AND :NEW.ORARIO_ARRIVO IS NOT NULL THEN
        :NEW.DURATA_CORSA := NUMTODSINTERVAL(:NEW.ORARIO_ARRIVO - :NEW.ORARIO_PARTENZA, 'DAY');
    END IF;
END;




